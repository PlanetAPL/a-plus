ãã Copyright (c) 1990-2008 Morgan Stanley All rights reserved.
ãã See .../src/LICENSE for terms of distribution.

$cx vim

blanks{x}:+/@1^\@1 x=' '

set_action{s;d;i;p;c;v}:if (`sym=©p) d¡{c}

up{c}:
	{
	nû0ÓÓ/mûblanks{c%`_VIEW};
	if (n<Ó/c%`_N) reset{c;1;n;m};
	}

down{c}:
	{
	nû0ÓÓ/mûblanks{c%`_VIEW};
	if (n>0) reset{c;¢1;n;m};
	}

max{c}:
	{
	nû0ÓÓ/mûblanks{c%`_VIEW};
	if (n<Ó/c%`_N) reset{c;Inf;n;m};
	}

zero{c}:
	{
	if (0=#c%`_MATRIX) û();
	nû0ÓÓ/mûblanks{c%`_VIEW};
	if (0=#m) set{c} else if (n>0) reset{c;0;n;m};
	}

set{c}:
	{
	(c,`_VIEW) .has (`freeze;1);
	(c%`_VIEW)û(0=blanks{c%`_MATRIX})/c%`_MATRIX;
	(c,`_VIEW) .has (`firstrow;0;`row;0;`freeze;0);
	}

reset{c;p;n;m}:
	{
	(c,`_VIEW) .has (`freeze;1);
	(fr;r)û0Ó¡`firstrow `row .of c,`_VIEW;
	lbûif (0=#c%`_VIEW) 0 else blanks{r#c%`_VIEW};
	if (p=¢1) lbûlb-lb=n else if (p=0) lbû0;
	opû¢1++/lb=(1+r)Ùm;
	boolûcase (p)
		{
		Inf;	(#c%`_N)Ò1;
		0;	0=c%`_N;
			(n+p)¦c%`_N
		};
	(c%`_VIEW)ûbool/c%`_MATRIX;
	nfrû0Ónr-r-fr Ý nrûop#(lb=blanks{c%`_VIEW})/É#c%`_VIEW;
	(c,`_VIEW) .has (`firstrow;nfr;`row;nr;`freeze;0);
	(c%`_BOOL)ûbool;
	}

fg{s;d;i;p;c;v}:((#c%`_COLORS)|n)#c%`_COLORS Ý nûblanks{d}

set_matrix{s;d;i;p;c;v}:zero{c}
preset_matrix{s;d;i;p;c;v}:if (~i½()) d else (~^/@1 d=' ')/d

preset_colors{s;d;i;p;c;v}:
	{
		if (~i½()) 		d 
	else 	if (d½()) 		s.shade{10;`cyan `magenta},1Õs.shade{10;`magenta `yellow}
	else	if ((`sym=©d)^1¨#Òd) 	,d
	else	if ((`char=©d)^2¨#Òd) 	ô1!d
	else				d
	}

file{c}:s.write{c%`_FILE;c%`_MATRIX}

define{cx}:
	{
	WSûs.autows{()};

	(cx%`_FILE)û'/tmp/vim.txt';

	(cx%`_MATRIX)û0 0Ò'';
	cxâ'_N:+/@1^\@1 _MATRIX=" "';
	(cxÖ`_MATRIX) _spcb (preset_matrix;);
	(cxÖ`_MATRIX) _scb (set_matrix;);

	cxâ'_BOOL:_N=0';

	(cxÖ`_COLORS) _spcb (preset_colors;);
	(cx%`_COLORS)û();
	(cx%`_VIEW)û0 0Ò'';

	(cx%`_ACTION)û(`zero `max `up `down `file;<@0(zero;max;up;down;file));

	(cx%`_VIM)û(`_ACTION;`_VIEW);

	(cx,`_VIEW) .is `view;
	(cx,`_ACTION) .is `action;
	(cx,`_VIM) .is `layout;

	(cx,`_VIEW) .has (`title;'';`rows;10;`cols;50;`fg;fg;`rowbg;`grey40;`bg;`black);
	(cx,`_ACTION) .has (`title;'';`label;('Level=0';'Level=Ó';'Level+1';'Level-1';'File');`acceptfocus;0;`geometry;`horizontal;`set;set_action);
	(cx,`_VIM) .has (`title;'');
	
	s.autows{WS};

	cxÖ`_VIM
	}

