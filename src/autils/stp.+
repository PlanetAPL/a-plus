ãã Copyright (c) 1990-2008 Morgan Stanley All rights reserved.
ãã See .../src/LICENSE for terms of distribution.

$cx stp

STPû'{^`STP;'

stop{y;x}:
	{
	if (`null=tû©x) û();
	zûif (kû`func=t) îx else x;
	iûzÉ':';
	(c;f)ûif (k) Ö_name{x} else ÖÂiÙz;
	(a;b)û((1+i)Ùz;(1+i)Õz);
	if (y) {if (~STP½(#STP)Ùb) câzûa,STP,b,'}'} else {if (STP½(#STP)Ùb) câzûa,(#STP)Õ¢1Õb};
	if (k) <{c%f} else z
	}

out_sym{s;d;i;p;c;v}:if (`sym¨©d) '' else Îd

stop_scv{s;c;v}:{^`STP}
stop_sdipcv{s;d;i;p;c;v}:{^`STP}

select_cx{s;c;v}:
	{
	if (¢1=iû0Ø`row .of c,v) û();
	(c%`_CX)ûcxûi#c%v;
	varsûvariables{c};
	(syms;vals)ûc%`_STOPPED;
	(syms;vals)û(<cx=(;0)#Ösyms)/¡(syms;vals);
	bûvals=`;
	symsûb/syms;
	idxû(cxÖvars)Ésyms;
	(c%`_VARS)ûvars;
	(c,`_VARS) .has (`index;idx;`row;¢1);
	}

select_vars{s;c;v}:
	{
	if (¢1=iû0Ø`row .of c,v) û();
	cxûc%`_CX;
	(c%`_VAR)ûvarûi#c%v;
	attsûattributes{c};
	valsûpick{cxÖvar;c%`_STOPPED};
	idxûattsÉvals;
	(c%`_ATTS)ûatts;
	(c,`_ATTS) .has (`index;idx;`row;¢1);
	}

refer_vars{s;c;v}:
	{
	if (¢1=iû0Ø`row .of c,v) û();
	cxûc%`_CX;
	varûi#c%v;
	defû_def{cx,var};
	sfûc%`_STOPPED;
	idxû0Ø`index .of c,v;
	if (iÅidx)
		{
		(varØsf)û(`¨vals)/valsûvarØsf;
		idxû(idx¨i)/idx;
		if (`char=©def) stop{0;def};
		}
	else
		{
		if (`char¨©def) û();
		stop{1;def};
		if (varÅ0Øsf) (varØsf)û(varØsf),` else sfûsf,¡(var;,<`);
		idxûidx,i;
		};
	(c%`_STOPPED)ûsf;
	(c,v) .has (`index;idx);
	}

stop_fun{var;att}:if (0Ø`bound .of var) (`scv `sdipcvÉs.typeOf{0Ø`class .of var;att})Ø(stop_scv;stop_sdipcv;)

refer_atts{s;c;v}:
	{
	if (¢1=iû0Ø`row .of c,v) û();
	cxûc%`_CX;
	varûc%`_VAR;
	attûi#c%v;
	(fun;cd)û2Ù0Øatt .of cx,var;
	sfûc%`_STOPPED;
	idxû0Ø`index .of c,v;
	if (iÅidx)
		{
		(varØsf)û(att¨vals)/valsûvarØsf;
		idxû(idx¨i)/idx;
		if (`func=©fun)
			{
			cbfûif (~_name{fun}Å`stp.stop_scv `stp.stop_sdipcv) stop{0;fun},<cd;
			(cx,var) .has (att;cbf);
			};
		}
	else
		{
		cbfûif (`func=©fun) stop{1;fun},<cd else stop_fun{cx,var;att};
		if (`func¨©cbf) û();
		(cx,var) .has (att;cbf);
		if (varÅ0Øsf) (varØsf)û(varØsf),` else sfûsf,¡(var;,<`);
		idxûidx,i;
		};
	(c%`_STOPPED)ûsf;
	(c,v) .has (`index;idx);
	}

unique{z}:((zÉz)=É#z)/z
pick{s;sf}:if (sÅ0Øsf) sØsf

contexts{}:(èÎz)#z Ý zû` _nl `cxs
variables{c}:(èÎz)#z Ý zûunique{_nl{cx;`vars},_nl{cx;`deps}} Ý cxûc%`_CX

attributes{c}:
	{
	(cx;var)ûc%¡`_CX `_VAR;
	if (0Ø`bound .of cx,var)
		{
		clsû0Ø`class .of cx,var;
		atsûs.attributes{cls};
		atsû(s.typeOf{cls;ats}Å`scv `sdipcv)/ats;
		(èÎats)#ats
		}
	}
 
define{cx}:
	{
	WSûs.autows{()};

	(cx%`_STOPPED)û(;);
	(cx%`_CX)û`;
	(cx%`_VAR)û();

	(cx%`_CXS)û();
	(cx%`_VARS)û();
	(cx%`_ATTS)û()
;
	(cx%`_STP)û<`_CXS `_VARS `_ATTS;

	(cx,`_CXS) .is `array;
	(cx,`_VARS) .is `array;
	(cx,`_ATTS) .is `array;
	(cx,`_STP) .is `layout;

	(cx,`_CXS) .has (`title;'Contexts';`space;20;`out;out_sym;`protect;1;`select;select_cx);
	(cx,`_VARS) .has (`title;'Variables';`space;20;`out;out_sym;`protect;1;`select;select_vars;`refer;refer_vars);
	(cx,`_ATTS) .has (`title;'Attributes';`space;20;`out;out_sym;`protect;1;`refer;refer_atts);
	(cx,`_STP) .has (`title;'';`C;1);

	(cx%`_CXS)ûcontexts{};
	(cx%`_VARS)ûvariables{cx};
	(cx%`_ATTS)ûattributes{cx};

	s.autows{WS};

	cxÖ`_STP
	}

