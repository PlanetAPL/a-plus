ãã Copyright (c) 1990-2008 Morgan Stanley All rights reserved.
ãã See .../src/LICENSE for terms of distribution.
$wa 60
$load s
ã sankar
$cx tool


(f sLash)a:{ã the generalized / operator
               
               iû¢1+1ÙÒa;
               rûa[0];
               i do rûr f a[i+1];
               r
            }

n(f nsLash )a:{ã the generalized "n g /" operator  (ie: n-wise reduction
                 vûÒÒa;
                 aû(-|n)!a;
           if (n<0) aû÷a;
                 f sLash @vÛ a
              }

a (f Cu ) b:{ã fix clumsiness of A+ ¡
             <(>a)f(>b)
             }

(f sCan)a:{ã generalized \ (scan operator
             iû¢1+1ÙÒa;
             rû,a[0];
             i do rûr,r[i] f a[i+1];
             r
           }

a(f oUtprod )b:{ ã generalized outer product
               iûÒAû,a;
               jûÒBû,b;
               rû();
               i do j do rûr,A[i]f B[j];
               ((Òa),Òb)Òr    
               }

a(f iNprod g)b:{ã generalized inner product with dimension matching
 if (0=ÒÒa) aû,a; if (0=ÒÒb) bû,b;
 if ((Aû¢1ÙÒa)¨Bû1ÙÒb)
             {
             cûAÄB;
             if (c¨A) aû(c-A)Õ@1Ûa;
             if (c¨B) bû(c-B)Õb;
             };
	Aû1ÕÉÒÒa;
        Bû1ÕÉÒÒb;
        Aûif (1<ÒÒa),<@AÛa else ,<,a;
        Bûif (1<ÒÒb) ,<@BÛôb else ,<,b;
        rû,A((g tool.Cu)tool.oUtprod)B; 
        rû>(f tool.sLash)¡r;
        rû((vû¢1ÕÒa),wû÷1ÕÒb)Òr;
        vû(ÉÒv),(Òv)+÷ÉÒw;
        do rûvôr;
               r}

Without{a;b}:{ãAPL2 dyadic ~ 
         cû~aÅb;
         c/a
              }

eTrange{a}:{a[0]+É(¢1Ùa)-a[0]}

Enc{a;b}:{ã APL2 partioned enclose;
           aû0,a;
          (1ÕÚ1,a¨0,¢1Õa)Úb
          }

Ifbad{expression}:{
ãreturns 1 if expression cannot be done;
û`box¨©0âexpression}
        
Round{a;b}:{
ãrounds b to a; .001 Round 56.78879 gives 56.799;
a«Ä.5+bßa}

Uniq{x}:{û((xÉx)=É#x)/x}

Rjust{data}:{ûô(-+/^\÷ôdata=' ')÷ôdata}

Ljust{data}:{ûô(+/^\ôdata=' ')÷ôdata}

uNcomment{text}:{ã remove comments from a text matrix;
      aûtext _ss 'ã';
      if (0=¢1ÙÒa)ûtext;
      iû¢1+Òrowsû0#a;
      sizû¢1ÙÒtext;
      colsûtool.eTrange ¡(1#a),¡()Ò siz;
      i do { text[rows[i];iØcols]û' '};
      text
          }


$cx doc


doc.readmat{fname}:{
  aû20Õ44Ù,usg.sysgetresult{'ls -l ',,fname};
  indû(ØÂ¡doc.pglist)ÉÂfname;
  if (a½ind#doc.daterec)  ûsys.readmat fname;
  (ind#doc.daterec)ûa;
  b û doc.pro {fname};
 Õ(,fname),' Modified';
  pûdoc.pgnamesÉ(<@1Û0Øb)tool.Without¡' ';
  qûp<#doc.pgnames;
  rû(q/p);
  (r#doc.pgrange)ûq/1Øb;
  .doc_data
  }

doc.fixfmt {arg}:{
       vû(argÅ' /')/ ÉÒarg;
       wû12«1+É(Ó(Òarg)ß12);
       oûw - tool.oUtprod v;
       oûo+(o<0)«1e9;
       o1ûÄ tool.sLash ôo;
       o1û|2 - tool.nsLash tool.Uniq 0,((Ø(<@1o)É¡o1)#v),Òarg;
       (Ó/o1)Ù¡o1Úarg}

doc.children{funcnum}:{
     iûfuncnum;
     doc.iiûdoc.ii+1;
     perûÄ10000«doc.iißdoc.count;
     if (0=500|per)(¢12Ù.lbl)û12Ù'/done=',(perûîperß100),'%';
     doc.newfilû(iØdoc.pgfile)tool.Without ' ';
     if (~doc.newfil½doc.oldfil){
          doc.oldfilûdoc.newfil;
           doc.rawdataûdoc.readmat doc.newfil};
      pgmtextû tool.uNcomment doc.rawdata[tool.eTrange i#doc.pgrange];

      vûdo
      {(g;h)û0#ôpgmtext _ss ':';
          pgmtextû<@1 pgmtext;
          (gØpgmtext)ûhÕgØpgmtext;
          pgmtextû>(()ÒÓ/ØÒ¡pgmtext)Ù¡pgmtext;
       };
      if (0¨0Øv) ûÉ0;
      bûÉ0;
      aû1#@1>Ò¡xû(<pgmtext) _ss ¡ doc.pgnamesx;
      do {bû1#@1>Ò¡yû(<pgmtext) _ss ¡ doc.pgnamesy};
      bûdoc.aa\b;
      vû((a©b)/ÉÒa);
      tool.Uniq v tool.Without funcnum}

doc.parents{funcno}:{
     aû,>©/¡funcno Å ¡ doc.childnos;
     aûa/ÉÒa;
     tool.Uniq a tool.Without funcno}

doc.getbsslist{}:{
    namesû();descû();
    badû<' ;"',doc.tab;
    a1ûsys.readmat¡( (<'/tmp/manydoca.+'),¡¢1Ù¡î¡ÉÒdoc.dirs);
    acû>Ò¡a1;ac[;1]ûÓ/ac[;1];
    a1û>, tool.Cu tool.sLash ac[;1]Ù@1¡a1;
    acû,Ø(0#ôac)/¡<¡doc.dirs;
    a2û1+,>¢1Ù¡(<@1a1='/')/¡<É¢1ÙÒa1;
    a2ûa2Õ¡<@1Ûa1;
    a2ûa2 tool.Without ¡bad;
    vû(Ø(Ò¡a2)-¡(÷¡a2)É¡'.')Õ¡a2;
    vvû1Ò¡'sa+';
    vvvû©/vv ½ tool.oUtprod v;  ã Finally a use for the defined operator
    acûvvv/ac;
    a2ûvvv/a2;
    aû2+>namesÉ¡'.';
    namesû(Ó/,>Ò¡a2)Ù¡a2;
    descû>(Ònames)Ò<20Ò' ';
     nû<ènamesû>names;
    (names;desc;ac)ûn#¡(names;desc;ac)
    
    }	

doc.makelist{names;dir}:{
    ã find functions; their line number range 
    filesûdir,¡names;
    pgmsûdoc.pro¡ files;
    vûØÒ¡Ò¡0Ø¡pgms;
    vû(2=v)/ÉÒv;
    pgmsûv#pgms;
    pgnamesû(<@1Ø0Ø¡pgms)tool.Without ¡`char©32;
    pgrangeûØ1Ø¡pgms;
    pgfileûØ2Ø¡pgms;
    vûÓ/,>Ò¡pgfile;
    vû<è>vÙ¡pgfile;
    v#¡(pgnames;pgrange;pgfile;(Òpgnames)Òdir)}

doc.pro{datafile}:{
    ãfind function headers
    .doc_dataûdataûsys.readmat datafile tool.Without `char©32;
    resû(;;;;);
    if (0=Òaû(©/@1Ûdata=':')/É1ÙÒdata)ûres;
    bû<@1Ûdata[a];
    cû>bÉ¡':';
    kû'ã'¨Ø0#¡b;
    dû1+>bÉ¡'}';
    bûcÙ¡b;
    nûk^~Ø'â'Å¡b;
    iû(n^c=d)/ÉÒa;
    vû(n^c=d)/a;
    if(0¨ 0Ødo vû(Év[0]¨0),v)ûres;
    jû-2 - tool.nsLash v,1ÙÒdata;
    jjû¢2!+\0,j;
    tlsû(b[i]É¡'{')Ù¡b[i];
     ordûè>40Ù¡tls;
    resû(ord#>40Ù¡tls;ord#(-Òi)Ùjj;ord#(Òi)Ò<datafile;ord#(-Òi)Ùj;-Òi);
    res
        } 

doc.ShowPageCB{var}:{
    iû()Ò1Ù,>`selected .of var;
    vû((iØdoc.dirs),,i#doc.names) tool.Without ' ';
    vû(1+(Òv)-(÷v)É'.') Ùv;
    (doc.page;j;d;jh;l)ûdoc.pro { v};
    do {doc.dataûlÙjhÚdoc.readmat v};
    
ã     .show `doc.page;
     `doc.page .has (`title;doc.fixfmt v);
       }

doc.ShowTextCB{val}:{
    if ((Âval)=`doc.page) {doc.stackûdoc.stack1û()};
  do{  iû1Ù,>`selected .of Âval;
    dûdoc.pgnamesÉ<thisû(,i#âval) tool.Without `char©32;};
   if ((Âval)=`doc.search) {
      dûdoc.pgnamesxÅ<thisû(,doc.searchfunc) tool.Without `char©32;
      eûdoc.aa\doc.pgnamesyÅ<this;
      dûif (0¨+/d) 1Ùd/ÉÒd else if (0¨+/e) 1Ùe/ÉÒe else Òd
                           };
    if (d=Òdoc.pgnames)û0;
    diûdoc.readmat doc.thisfilû(vvûdØdoc.pgfile)tool.Without `char©32;
    doc.txtûdi[tool.eTrange ,doc.thisrangeûd#doc.pgrange];
    k1ûdoc.txt=doc.tab;
    k1û(<@1Ûk1)/¡<Évû¢1ÙÒdoc.txt;
    k2û¢1+1ÙÒdoc.txt;
    k2 do { if (0¨Ò>k1[k2]){
              aûdoc.txt[k2;];bû(k2Øk1);
              bû- - tool.sLash @1Û¢2!0,b,v;
              cûbÚa;
              ãiûÒb;
              bû8«Ó1Ó((b+.1)ß8);
              doc.txt[k2]ûvÙ(ØbÙ¡c tool.Without ¡doc.tab);
               }};
        vûÒdoc.txt;
        ãvû100Ä¢1Ùv;
        vû¢1Ùv; 
        doc.textûvÙ@1doc.txt;
      (doc.pgmsins;doc.pgmsin)û(;);
do{
  if (~(<this)Ådoc.stack) doc.stackûdoc.stack,<this
         else  doc.stackû(1+-(Òdoc.stack)-doc.stackÉ<this)Õdoc.stack;
  if (~(<this)Ådoc.stack1) doc.stack1ûdoc.stack1,<this
          else  doc.stack1û(1+-(Òdoc.stack1)-doc.stack1É<this)Õdoc.stack1;
};
if (0¨ÒdØdoc.childnos)
      {
      doc.pgmsinsûtool.Uniq >40Ù¡cû((dØdoc.childnos)#doc.pgnames)tool.Without <this;
      doc.pgmsinsû(lûèdoc.pgmsins)#doc.pgmsins;
      ccolû(Òcûl#c)Ò`black;
    do  ((cÅdoc.stack1)/ccol)û`green;
      `doc.pgmsins .has (`fg;ccol);
     ã .shw `doc.pgmsins .is `array;
      
      `doc.pgmsins .has (`title;doc.fixfmt "children ",this;`refer;(doc.ShowTextCB;"doc.pgmsins"));
       };
 do{`doc.pgmsins .has (`title;doc.fixfmt "children ",this)};

if (0¨ÒdØdoc.parentnos)
      {
      doc.pgmsinûtool.Uniq >40Ù¡cû((dØdoc.parentnos)#doc.pgnames)tool.Without <this;
      doc.pgmsinû(lûèdoc.pgmsin)#doc.pgmsin;
	ccolû(Òcûl#c)Ò`black;
      do ((cÅdoc.stack)/ccol)û`red;
      `doc.pgmsin .has (`fg;ccol);
      ã.show `doc.pgmsin .is `array;
      
      `doc.pgmsin .has (`title;doc.fixfmt "parents ",this;`refer;(doc.ShowTextCB;"doc.pgmsin"));
       };
  do{`doc.pgmsin .has (`title;doc.fixfmt "parents ",this)};
    
     ã.show `doc.text;
     doc.colû(1ÙÒdoc.text)Ò`black;doc.xyûÉ0;
     
     do {
        vvv û(<@1Ûdoc.pgmsins)tool.Without¡' ';
         vvvûtool.Uniq vvv,((ØÒ¡vvv)-Ø(÷¡vvv)É¡'.')Õ¡vvv;
     doc.xyûØ0#¡(<tool.uNcomment doc.txt)_ss¡  vvv};
     if (0¨Òdoc.xy) doc.col[doc.xy]û`blue;
     `doc.text .has (`fg;doc.col;`title; vv;`f9;doc.print);
     }

doc.print{}:{usrûsys.getusername{};
	`docâ'$|text apr '
	}
doc.getfunc{s;d;i}:{
doc.ShowTextCB "doc.search"


}
doc.getext{s;d;i}:{
       pûdo nû1Ù(v>,>`row .of `doc.text)/vû0# doc.text _ss doc.searchtext tool.Without `char©32;
                  
       do  `doc.text .has (`row;n)
       }

doc.savefile{s;d;i}:{
 do {
   if (0=Òdataû<@1 sys.readmat doc.thisfil)û();
   insertû<@1 doc.text;
   dataû(doc.thisrange[0]Ùdata),insert,(doc.thisrange[1]Õdata);
   dataûØdata,¡`char©10;
   fnameû'/u/',sys.getusername{},'/doctmp/',((Òdoc.thisfil)-(÷doc.thisfil)É'/')Õdoc.thisfil;
   if (0<vûsys.creat{fname;8 8 8Â6 6 6}) sys.open{fname;;6};
   sys.write{v;data;Òdata};
   sys.close{v}
   }
}

doc.loadfunc{s;d;i}:{
 do {
   if (0=Òdataû<@1 doc.readmat doc.thisfil)û();
   insertû<@1 doc.text;
   dataû(doc.thisrange[0]Ùdata),insert,(doc.thisrange[1]Õdata);
   dataûØdata,¡`char©10;
   fnameû'/u/',sys.getusername{},'/doctmp/TMP',((Òdoc.thisfil)-(÷doc.thisfil)É'/')Õdoc.thisfil;
   if (0<vûsys.creat{fname;8 8 8Â6 6 6}) sys.open{fname;;6};
   sys.write{v;data;Òdata};
   sys.close{v};
   _load fname;
 ã  â'$rm ',fname
   }

}
doc.findind{a;b}:{
(a=<b)/ÉÒa}

doc.choose{a;b}:{
  vûdo a#b;
  if (0=0Øv) 1Øv else ' '
   }


usg.getusername{}:
	{	ã   /n info ...
        useridûsys.getusername{};
	if (`sym=©userid)	useridû,Îuserid;
	 pû':',,usg.sysgetresult {'msdir -e ',userid};
	 pû1Õ¡1Õ((-0#q)+1#qûô¢2!0,(p=':')/É#p)Úp;
         pûØ2Ùp,¡' '
	}

usg.sysgetresult{com}:{
       sys.system{com,' >/tmp/manydocx'};
       sys.readmat '/tmp/manydocx'
                    }
doc.grep{s;d;i}:{
â'$grep ',(vû,doc.searchtext tool.Without ' '), ' ',(0Ødoc.dirr),'  >/tmp/manypqr';
if ((Òdoc.dirr)>1) (iû¢1+Òdoc.dirr) do â'$grep ',v, ' ',((i+1)Ødoc.dirr),' >>/tmp/manypqr';
xûdoc.pgfile tool.Without ¡' ';
aû<@1 sys.readmat{'/tmp/manypqr'};
oû(~Ø':'Å¡a)/É#a;
(o#a)û(<(0Ødoc.dirr),':'),¡o#a;
vvû(tool.Uniq (aÉ¡':')Ù¡a)tool.Without¡' ';
fnamesûtool.Uniq (x Å vv)/x;
pû(<x) doc.findind ¡ fnames;
doc.greplstû>(Ó/ØÒ¡b)Ù¡bûtool.Uniq Ø(0¨¡ Ø¡1#¡¡Ò¡¡((tool.eTrange¡¡<@1¡p#¡<doc.pgrange)doc.choose¡¡<¡doc.readmat¡fnames tool.Without¡' ') _ss ¡¡<<v)/¡p#¡<doc.pgnames;

`doc.greplst .has (`raise;1;`rows;10;`font;"kaplscreen-bold";`titlefont;"kaplscreen-bold";`refer;(doc.ShowTextCB;Î`doc.greplst);`shelltitle;"Functions with text";`title;"Select to see Listing" ;`raise;1);

.show `doc.greplst .is `array;
}

doc.unqfix{vect}:{
ûvect;
aû(vectÉvect)¨ÉÒvect;
cû1+É+/a;
bûÓ/ØÒ¡vect;
(a/vect)û(bÙ¡a/vect),¡,¡(<'i2')_fmt¡ c;
uû~a;
(u/vect)ûbÙ¡u/vect;
vect}

doc.main{dir}:{ã sankar 11/2/94
ã eg:  doc.main './manydoc.+'

if (`char=©dir) dirû,<dir;
doc.dirrûdoc.dirsûdir;
if (doc.dirs½())
{doc.dirsû('/usr/local/aplus-fsf-4.22/autils/*.*';'/usr/local/aplus-fsf-4.22/lib/*.+')};
â¡vû(<'$ls -l '),¡doc.dirs, ¡(<' > /tmp/manydoca.+'),¡¢1Ù¡î¡,ÉÒdoc.dirs;
doc.dirsû(-Ø(÷¡doc.dirs)É¡'/')Õ¡doc.dirs;
â'$sleep 2'; ã too lazy to use adap - hope this works
doc.tabû`char©9;
doc.listû`doc.names`doc.desc;
datûdoc.getbsslist{};
doc.namesû0Ødat;
doc.descû1Ødat;
doc.dirsû2Ødat;
(a;b;c;d)ûdoc.makelist{<@1doc.names;doc.dirs};
aûdoc.unqfix a;
(doc.pgnames;doc.pgrange;doc.pgfile;doc.pgfiles)û(a;b;c;d);
doc.daterecû>20Õ¡44Ù¡,¡usg.sysgetresult¡(<'ls -l '),¡doc.pglistû((Ø¡doc.dirs),¡<@1Ûdoc.names)tool.Without¡' ';
doc.pgnamesxûdoc.pgnames;
doc.aaû>©/¡(doc.pgnames) Å ¡ '.';
doc.pgnamesyû(1+Ø(doc.aa/doc.pgnames)É¡'.')Õ¡doc.aa/doc.pgnames;
doc.treegetû1;
if (doc.treeget)
{
.lblû'Wait...Building Tree',12Ò' ';
doc.countûÒdoc.pgnames;
doc.iiû0;
`.lbl .has (`shelltitle;'';`title;'';`fg;`red;`bg;`blue;`naturalsize;1);
.show `.lbl ;
doc.rawdataû(2 80Ò' ');doc.oldfilû5Ò' ';
doc.childnosûdoc.children¡ÉÒdoc.pgnames;
doc.parentnosûdoc.parents ¡ÉÒdoc.pgnames;
.free `.lbl;
};
`doc.names .has (`title;"FileName");
`doc.list .is `table;
`doc.list .has (`refer;(doc.ShowPageCB;`doc.list));
`doc.page .is `array;
`doc.page`doc.list .has ¡ <(`protect;1); 
`doc.page .has (`refer;(doc.ShowTextCB;'doc.page'));
`doc.page .has (`rows;10);
doc.pageûdoc.pgmsinsûdoc.pgmsinû10 20Ò' ';
doc.textû30 100Ò' ';
(`docÖ`pgmsin`pgmsins) .is  ¡`array;
`doc.text .is `array;
`doc.text .has (`rows;25;`rowsep;0);
doc.searchtextû'    ';
`doc.searchtext .is `scalar;
`doc.searchtext .has (`title;"text search";`set;doc.getext;`refer;doc.getext);
doc.searchfuncû'      ';
`doc.searchfunc .is `scalar;
`doc.searchfunc .has (`title;"func search";`set;doc.getfunc);
doc.loadû(doc.loadfunc;);
doc.saveû(doc.savefile;);
doc.Grepû(doc.grep;);
`doc.load`doc.save`doc.Grep .is ¡ `button;
`doc.load`doc.save`doc.Grep .has ¡ <(`titlefont;"kaplscreen-bold");
doc.searchû1 5Ò`docÖ`searchtext`Grep`searchfunc`load`save;
`doc.search .is `layout;
`doc.search .has (`title;'');
`doc.text .has (`font;"kaplscreen-bold");
ã.show `doc.list;
`doc.list`doc.page`doc.pgmsins`doc.pgmsin`doc.text`doc.searchtext`doc.searchfunc .has¡<(`font;"kaplscreen-bold";`titlefont;"kaplscreen-bold");
ã`doc.list .has (`title;'';`icontitle;"BssDoc");
doc.allû((<1 4Ò`doc.list`doc.page`doc.pgmsins`doc.pgmsin),2 1Ò`doc.search`doc.text);
 `doc.all .is `layout;
`doc.all .has (`title;"Script Browser ",Ødir,¡' ';`refresh;1;`font;"kaplscreen-bold";`titlefont;"kaplscreen-bold";`icontitle;"BssDoc");
.show `doc.all
    }

.lblû' '
.show `.lbl .is `label
'eg:'
'  doc.main "./manydoc.+"'
'  doc.main {("./manydoc.+";"./dio.+")}'
'  '
' Use refer event (Double click) to list of functions in script files'
' Double click on any displayed function list will cause function to display'
' Grep searches for character string in the whole appln; displays list of fns'
' "Parents" and "Children" of displayed function shown; these can be double clicked'
 
