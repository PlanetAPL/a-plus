ãã Copyright (c) 1990-2008 Morgan Stanley All rights reserved.
ãã See .../src/LICENSE for terms of distribution.

$cx tim

out_sym{s;d;i;p;c;v}:if (()½d) '' else Îd
set_exp{s;d;i;p;c;v}:report{c} Ý if (0<#d) do if (':'¨1Ùd) âd else manual_refer{c;1Õd}

set_raise{s;d;i;p;c;v}:
	{
	case (p)
		{
		`Report;(c,`_CHART) .has (`freeze;1) Ý (c,`_REPORT) .has `raise;
		`Chart;	(c,`_CHART) .has (`freeze;0;`raise;1)
		}
	}

set_types{s;d;i;p;c;v}:select_cx{s;c;`_CXS}

selectcol_time{s;c;v}:(c%`_SORT)û0Ø`col .of c,v
selectcorner_time{s;c;v}:(c%`_SORT)û¢1

set_sort{s;d;i;p;c;v}:
	{
	fûd>¢1;
	(c,`_REPORT) .has (`colindex;if (f) d else É0;`cornerindex;~f);
	sort_report{c;d};
	}

sort_report{c;k}:
	{
	if (2=#tûc%`_REPORT) û();
	if (k=¢1)
		{
		(c%`_LABELS)[]û(2Ùl),i#2Õl Ý iûèÎ2Õl Ý lûc%`_LABELS;
		(c%`_REPORT)[]û1.0«(2Ùt),i#2Õt Ý tûc%`_REPORT;
		(c%`_REPORT)[;5]û0.0;
		}
	else
		{
		iûç(;k)#2Õt;
		t[]ût[É2],i#2Õt;
		t[;5]û100.0,100«zdiv{1Õt[;k];t[0;k]};
		(c%`_LABELS)[]û(2Ùl),i#2Õl Ý lûc%`_LABELS;
		(c%`_REPORT)û1.0«t;
		};
	}


COL_LABELSû('Calls';'User';'System';'Elapsed';'Total CPU';'%Total')

label_fmt{i}:if (i¤1) 6Ò' ' else 6Ù(1Õî¢2+i),'.'
label_time{s;d;i;p;c;v}:if (()½i) '' else if (()½0Øi) (1Øi)ØCOL_LABELS else n,m Ý mûÎ(0Øi)#c%`_LABELS Ý nûlabel_fmt{0Øi}

all{c}:
	{
	(c%`_TIMED)û();
	(iû#cxs Ý cxsûc%`_CXS) do (c%`_TIMED)[,]û(i#cxs)Öfunctions{c;i#cxs};
	(c,`_CXS) .has (`index;É#c%`_CXS);
	(c,`_FNS) .has (`index;É#c%`_FNS);
	}

manual_refer{c;d}:
	{
	if (0<#d)
		{
		obsû>Â¡(t¨¡' ')/¡tû1Õ¡(Úd=',')Údû',',d;
		cxûc%`_CX;
		tiûc%`_TIMED;
		(cxs;fns)û<@1ôÖobs;
		if (1Åbûcxs=cx)
			{
			obsû(~b)/obs;
			fnsûb/fns;
			iû0Ø`index .of c,`_FNS;
			pbsûcxÖfns;
			bbûpbsÅti;
			remûbb/pbs;
			addû(~bb)/pbs;
			tiû(~tiÅrem)/ti;
			ti[,]ûadd;
			(c,`_FNS) .has (`index;(tÅti)/É#tûcxÖc%`_FNS);
			};
		bûobsÅti;
		remûb/obs;
		addû(~b)/obs;
		tiû(~tiÅrem)/ti;
		ti[,]ûadd;
		(c%`_TIMED)ûti;
		};
	}

refer_fns{s;c;v}:
	{
	(i;r)û`index `row .of c,v;
	if (r=¢1) û();
	cxûc%`_CX;
	if (rÅi) (c%`_TIMED)û(t¨cxÖr#c%v)/t Ý tûc%`_TIMED Ý iû(i¨r)/i else (c%`_TIMED)[,]ûcxÖr#c%v Ý iûi,r;
	(c,v) .has (`index;i);
	}

refer_cx{s;c;v}:
	{
	if (r=¢1 Ý rû0Ø`row .of c,v) û();
	cxûr#c%v;
	if (rÅi Ý iû0Ø`index .of c,v)
		{
		iû(i¨r)/i;	
		(c%`_TIMED)û(cx¨0#ôÖt)/t Ý tûc%`_TIMED;
		(c,`_FNS) .has (`index;É0);
		}
	else 	{
		iûi,r;
		fnsû(~jÅ0Ø`index .of (c,`_FNS))/fns Ý jûÉ#fns Ý fnsûc%`_FNS;
		(c%`_TIMED)[,]ûcxÖfns;
		(c,`_FNS) .has (`index;j);
		};
	(c,v) .has (`index;i);
	}

select_cx{s;c;v}:
	{
	if (¢1=r Ý rû0Ø`row .of c,v) û();
	(c%`_CX)ûcxûr#c%v;
	tfnsû(cx=tcxs)/tfns Ý (tcxs;tfns)û<@1ôÖc%`_TIMED;
	fnsûfunctions{c;cx};
	(c,`_FNS) .has (`index;fnsÉtfns);
	(c%`_FNS)ûfns;
	}

set_action{s;d;i;p;c;v}:
	{
	case (p)
		{
		`Start;		start{c};
		`Stop;		stop{c};
		`Report;	report{c};
		`Clear;		clear{c};
		`All;		all{c};
		`File;		file{c};
		`Refresh;	refresh{c};
		}
	}

file{c}:s.write{c%`_FILE;((-1#Òz)ÙØ¢12Ù¡tim.COL_LABELS),zû(Îc%`_LABELS),@1 Û 12îc%`_REPORT}

start{c}:report{c} Ý â'time ',Ø'`',¡Î¡t Ý (c%`_TIMED)ût Ý tû(èÎt)#t Ý tûc%`_TIMED
stop{c}:report{c} Ý â'time `'

timer{}:if (0=½t Ý tûtime{}) 1.0«2 4Ò1,t else if ((,`)½,0Øt) 1Õ¡t else t

timer_matrix{s;m}:
	{
	mû(+/m),m;
	mûm,@1 0 m[;1]+m[;2];
	mûm,@1 0 if ((s<1ÕÒm)^(s¦0)^s¤5) 100.0,100«zdiv{1Õm[;s];m[0;s]} else 0;
	m
	}

report{c}:
	{
	(l;r)ûif (0=½t Ý tûtimer{}) (2Ùc%`_LABELS;t) else ((2Ùc%`_LABELS),x;m) Ý mûtimer_matrix{c%`_SORT;1.0«m} Ý (x;m)ût;
	if ((2<#l)^`ContextsØc%`_TYPES)
		{
		(ll;rr)û2Ù¡(l;r);
		(l;r)û2Õ¡(l;r);
		(l;r)û(<èl)#¡(l;r);
		bû1,1Õd¨¢1÷dû(;0)#Öl;
		lûll,b/d;
		rûrr,>+/¡(Úb)Úr;
		};
	(c%¡`_LABELS `_REPORT)û(l;r);
	(c%`_SORT)ûc%`_SORT;
	}

clear{c}:
	{
	(c,`_CXS) .has (`index;É0);
	(c,`_FNS) .has (`index;É0);
	(c%`_TIMED)û();
	stop{c};
	}

zdiv{y;x}:if (x=0) (#y)Ò0 else yßx

chart{c;m;n}:if (2=#m) 1 7Ò0,1#m else ô(Ék),(ôp) zdiv @1 0Û+/p Ý pûm[1+Ék;] Ý kûnÄ¢1+#m

contexts{}:(èÎz)#z Ý zû` _nl `cxs

valid{x}:if (0=#x) É0 else ((;0)#Îx)Å'.',Ø3Ùs.ALPHA_NUM

functions{c;cx}:
	{
	tû((>1Øc%`_TYPES)É1)Ø(`fns;`deps;`fns `deps;`fns `deps);
	zû();
	if (`fnsÅt) z[,]ûvalid{>_name¡{cx%¡r}}/r Ý rûcx _nl `fns;
	if (`depsÅt) z[,]ûcx _nl `deps;
	(èÎz)#z
	}

refresh{c}:
	{
	clear{c};
	(c%`_CXS)ûcontexts{};
	(c%`_FNS)ûfunctions{c;`};
	}
 
define{cx}:
	{
	WSûs.autows{()};

	(cx%`_FILE)û'/tmp/tim.txt';

	(cx%`_LABELS)û(Â'*Totals*'),Â'*Unknown*';

	(cx%`_TIMED)û();
	(cx%`_CX)û`;

	(cx%`_SORT)û¢1;
	(cx,`_SORT) _scb (set_sort;);

	(cx%`_ACTION)û(`Start `Stop `Report `Clear `All `File `Refresh;(;;;;;;));
	(cx%`_RAISE)û(`Report `Chart;(1;0));
	(cx%`_TYPES)û(`Functions `Dependencies `All `Contexts;(1;0;0;0));
	(cx%`_CONTROLS)û<`_ACTION `_RAISE `_TYPES;
	(cx%`_CXS)ûcontexts{};
	(cx%`_FNS)ûfunctions{cx;`};
	(cx%`_OBJECTS)û<`_CXS `_FNS;
	(cx%`_REPORT)û2 6Ò0;
	cxâ'_REPORT_CHART:tim.chart{`',(Îcx),';_REPORT;_N}';
	(cx%`_CHART)û`_REPORT_CHART;
	(cx%`_DISPLAYS)û();
	(cx%`_BODY)û(`_OBJECTS;`_DISPLAYS);
	(cx%`_N)û10;
	(cx%`_EXP)û'';
	(cx%`_INPUTS)û<`_EXP `_N;
	(cx%`_TIM)û(`_CONTROLS;`_BODY;`_INPUTS);

	(cx,`_ACTION) .is `action;
	(cx,`_TYPES) .is `choice;
	(cx,`_RAISE) .is `choice;
	(cx,`_CONTROLS) .is `layout;
	(cx,`_CXS) .is `array;
	(cx,`_FNS) .is `array;
	(cx,`_OBJECTS) .is `hpane;
	(cx,`_CHART) .is `graph;
	(cx,`_REPORT) .is `matrix;
	(cx,`_DISPLAYS) .is `layout;
	(cx,`_BODY) .is `vpane;
	(cx,`_N) .is `scalar;
	(cx,`_EXP) .is `scalar;
	(cx,`_INPUTS) .is `layout;
	(cx,`_TIM) .is `layout;

	(cx,`_ACTION) .has (`title;'';`geometry;`horizontal;`C;1;`set;set_action;`acceptfocus;0);
	(cx,`_RAISE) .has (`title;'';`set;set_raise;`resize;'wW';`shadowthickness;0;`acceptfocus;0);
	(cx,`_TYPES) .has (`title;'';`set;set_types;`resize;'wW';`shadowthickness;0;`acceptfocus;0);
	(cx,`_CONTROLS) .has (`title;'';`resize;'hH';`shadowthickness;0);
	(cx,`_CHART) .has (`title;'';`legend;`tr;`freeze;1;`shadowthickness;0);
	(cx,`_REPORT_CHART) .has (`style;`bar;`legend;COL_LABELS);
	(cx,`_CXS) .has (`title;'Contexts';`space;37;`out;out_sym;`protect;1;`select;select_cx;`refer;refer_cx;`shadowthickness;0);
	(cx,`_FNS) .has (`title;'Functions';`space;37;`out;out_sym;`protect;1;`refer;refer_fns;`shadowthickness;0);
	(cx,`_OBJECTS) .has (`title;'';`shadowthickness;0);
	(cx,`_REPORT) .has (`title;'';`protect;1;`label;label_time;`colspace;29 9 9 9 9 10 9;`selectcol;selectcol_time;`selectcorner;selectcorner_time;`cornerindex;1;`shadowthickness;0;`cols;7;`out;(4Ò<'i9'),(<'i10'),<'f7.2');
	(cx,`_BODY) .has (`title;'';`shadowthickness;0);
	(cx,`_DISPLAYS) .has (`title;'';`position;0;`shadowthickness;0);
	(cx,`_EXP) .has (`title;'â: ';`set;set_exp;`shadowthickness;0);
	(cx,`_N) .has (`title;'N: ';`resize;'wW';`shadowthickness;0);
	(cx,`_INPUTS) .has (`title;'';`resize;'hH');
	(cx,`_TIM) .has (`title;'';`shadowthickness;0);

	(cx%`_DISPLAYS)û`_CHART `_REPORT;

	s.autows{WS};

	cxÖ`_TIM
	}

