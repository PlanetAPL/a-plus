ãã Copyright (c) 1990-2008 Morgan Stanley All rights reserved.
ãã See .../src/LICENSE for terms of distribution.

$cx box
	
ALPHA_NUMû('abcdefghijklmnopqrstuvwxyz';'ABCDEFGHIJKLMNOPQRSTUVWXYZ';'_';'0123456789';'¢';'$');

quoted{x}:(r©¢1÷rû2|+\'"'=x)©r©¢1÷rû2|+\''''=x
function_name{x}:if (0=0Øzûdo _name{x}) Î1Øz else îx
double_quote{x}:(1+x='''')/x

in{x}:
	{
		if 	(0=#xû,x)						û()
	else 	if 	(0=#zû(~quoted{x})/x)					zûâx
	else 	if 	(~©/(Ø1Ù¡1Õ¡(Úz=' ')Úzû' ',z)ÅØ0 1 2 5#ALPHA_NUM) 	zûâx
   	else 									Ù`domain;

	if (0¤½zûâx) z else Ù`domain
	}

out{x}:out_array{if (¢1=½x) <{x} else x;1}

out_array{x;y}:
	{
	oneû1=tsxû«/sxûÒx;
	smpû~bxdû0<½x;
	if (rflagû1Å1<tsx) if (rflagû^/,x=fxû0#1Ù,x) xûfx;
	zûcase (typû©x)
		{
		`int;		out_iota{x;''};
		`float;		out_iota{x;'1.0«'};
		`sym;		out_symVec{out_sym¡{,x}};
		`char;		'''',double_quote{,x},'''';
		`box;		if ('(())'½zû(one#'(<'),(1ÕØ';',¡&¡{,x;0}),(~one)/')') '()' else z;
		`func;		if (1=#,x) (y/'<{'),function_name{''Òx},y/'}' else '(',(1ÕØ';',¡&¡{,x;0}),')';
		`null;		'()'
		};
	if (rflag) ((1Õîsx),'Ò',bxd/'<'),z else (if (~smp^'É'Åz) if (1<#sx) (1Õîsx),'Ò' else if (((,1)½sx)^1=#sx) ','),z
	}

out_iota{x;y}:
	{
	if ((,0)½nûÒx) y,'0Ò0'
	else if (0Ån) y,'0'
	else if (0=#n) y,1Õîx
	else if (3>#vû,x) y,1Õîv
	else if (~x½i+(jûv[1]-iûv[0])«ÉnûÒx) y,1Õîv 
	else y,(if (i=0) '' else (1Õîi),'+'),(if (j=1) '' else (1Õîj),'«'),'É',1Õîn
	}

out_sym{x}:if (^/(zûîx)Å'.',Ø0 1 2 3#ALPHA_NUM) '`',z else 'Â''',z,''''

out_symVec{x}:
	{
		if 	(1=#x) 			Øx 
	else 	if 	(^/bû'`'=Ø,0Ø¡x) 	Øx 
	else
		{
		bû~b;
		(br;c)û(<b)/¡((¢1Õb>1÷b),0;x);
		cû(<',('),¡c,¡')';
		if (1Åbr) (br/c)û(br/c),¡',';
		(b/x)ûc;
		zûØx;
		(','=1Ùz)Õz
		}
	}
